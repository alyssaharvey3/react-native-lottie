buildscript {
  repositories {
    google()
    mavenCentral()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:7.0.4'
    classpath("de.undercouch:gradle-download-task:4.1.2")
  }
}

allprojects {
    repositories {
        maven {
            // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
            url("$rootDir/../node_modules/react-native/android")
        }
        maven {
            // Android JSC is installed from npm
            url("$rootDir/../node_modules/jsc-android/dist")
        }
        mavenCentral {
            // We don't want to fetch react-native from Maven Central as there are
            // older versions over there.
            content {
                excludeGroup "com.facebook.react"
            }
        }
        google()
        maven { url 'https://www.jitpack.io' }
    }
}

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

apply plugin: 'com.android.library'
apply from: 'gradle-maven-push.gradle'

android {
  buildToolsVersion safeExtGet('buildToolsVersion', "31.0.0")
  compileSdk safeExtGet('compileSdkVersion', 31)
  ndkVersion safeExtGet('ndkVersion', "21.4.7075529")

  defaultConfig {
    targetSdkVersion safeExtGet('targetSdkVersion', 31)
    minSdkVersion safeExtGet('minSdkVersion', 21)
  }
}

dependencies {
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-native:+" // From node_modules

  implementation "com.airbnb.android:lottie:${safeExtGet('reactNativeAndroidLottieVersion', '5.1.1')}"
}
